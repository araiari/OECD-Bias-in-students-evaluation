# Model:
# Y_ij = theta0 + gamma0_j + theta * Z_i + gamma_j * X_i + eps_ij

# Y_ij ~ Norm (theta0 + gamma0_j + theta * Z_i + gamma_j * X_i, tau2) i=1,...,N j=1,...,ngr
# tau2 ~ Gamma(2,10)
# theta0 ~ Norm (0, tau2_theta0); tau2_theta0 ~ Gamma(2,50)
# gamma0_j ~ Norm (0, tau2_gamma0_j); tau2_gamma0_j ~ Gamma(2,50) j=1,...,ngr
# theta ~ Norm (0, tau2_theta); tau2_theta ~ Gamma(2,50)
# gamma_jk ~ Norm (0, tau2_gamma_jk); tau2_gamma_jk ~ Gamma(2,50) j=1,...,ngr k=1,...,p

# Legend of symbols:
# N = total number of data
# p = number of covariates of random effect
# ngr = number of groups
# gr = vector whose elements allocate the data to a group
# Y = vector of response (pv5math)
# X = matrix of covariates about students
# Z = vector of the selected covariate about school
# gamma = param. of random effects (matrix p*ngr)
# gamma0 = intercept of random effects (vector 1*ngr)
# theta = param. effects from school (scalar)
# theta0 = intercept of fixed effect

model{

	# likelihood e log_likelihood per waic
	for (i in 1:N){
 		mu[i] <- theta0 + gamma0[gr[i]] + theta*Z[i] + inprod(X[i,], gamma[,gr[i]]) 
		Y[i] ~ dnorm ( mu[i], tau2 )
		lik[i] <- dnorm( Y[i], mu[i], tau2 )
	}

	# priors
	tau2 ~ dgamma(2, 10)
	
	tau2_theta0 ~ dgamma(2,50)
	theta0 ~ dnorm(0, tau2_theta0)
		
	tau2_theta ~ dgamma(2,50)
	theta ~ dnorm(0, tau2_theta)

	for (j in 1:ngr){
		tau2_gamma0[j] ~ dgamma(2, 50)
		gamma0[j] ~ dnorm(0, tau2_gamma0[j])

		for (i in 1:p){
			tau2_gamma[i,j] ~ dgamma (2,50)
			gamma[i,j] ~ dnorm(0, tau2_gamma[i,j])
		}
	}


}



